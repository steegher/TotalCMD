// Версия кликера 4.9+

#name "Большой тест v1.0"

$xmouse = $_xmouse
$ymouse = $_ymouse
$xmax = $_xmax
$ymax = $_ymax
$line = $_line
$cursor = $_cursor
$ms = $_ms
$timet = $_time_t
$timeh = $_time_h
$timem = $_time_m
$times = $_time_s
$datey = $_date_y
$datem = $_date_m
$dated = $_date_d
$hwnd = $_hwnd

IF(TFCOUNT ("Test_file.txt") > 0)
   TFCLEAR ("Test_file.txt", 1)
END_IF

IF(TFCOUNT ("Test_TFWRITE.txt") > 0)
   TFCLEAR ("Test_TFWRITE.txt", 1)
END_IF

IF(TFCOUNT ("Test INI.ini") > 0)
   TFCLEAR ("Test INI.ini", 1)
END_IF

LOGCLEAR
LOGWRITE ("======Тест системных переменных=======")
LOGWRITE ("Координаты мыши по оси Х: ", $xmouse)
LOGWRITE ("Координаты мыши по оси Y: ", $ymouse)
LOGWRITE ("Ширина разрешения экрана: ", $_xmax)
LOGWRITE ("Высота разрешения экрана: ", $ymax)
LOGWRITE ("Номер выполняемой строки сценария: ", $line)
LOGWRITE ("Код вида курсора мыши: ", $cursor)
LOGWRITE ("Системный таймер Windows: ", $ms)
LOGWRITE ("Время Unix: ", $timet)
LOGWRITE ("Текущий час: ", $timeh)
LOGWRITE ("Текущая минута: ", $timem)
LOGWRITE ("Текущая секунда: ", $times)
LOGWRITE ("Текущий год: ", $datey)
LOGWRITE ("Текущий месяц: ", $datem)
LOGWRITE ("Текущие число: ", $dated)
LOGWRITE ("Текущий hwnd: ", $hwnd)

LOGWRITE (" ")
LOGWRITE ("======Работа с числами=================")
LOGWRITE (" ")
LOGWRITE ("тест DEFINE, создание переменной def со значением 123456")
DEFINE ($def, 123456)
LOGWRITE ("def = ", $def)

LOGWRITE (" ")
LOGWRITE ("тест UNDEFINE, стирание переменной def со значением 123456")
UNDEFINE ($def)
LOGWRITE ("def = ", $def)

LOGWRITE (" ")
LOGWRITE ("тест INC (1,6,-94)")
INC($inc_test, 1)
LOGWRITE ($inc_test)
INC($inc_test, 5)
LOGWRITE ($inc_test)
INC($inc_test, -100)
LOGWRITE ($inc_test)

LOGWRITE (" ")
LOGWRITE ("тест RND")
$rnd = RND (1, 10)
LOGWRITE ("Рандом 1-10: ", $rnd)

LOGWRITE (" ")
LOGWRITE ("тест RNDFROM")
$rnd1 = RNDFROM(1,5,10)
LOGWRITE ("Рандом 1,5,10: ", $rnd1)

//LOGWRITE (" ")
//LOGWRITE ("тест RNDFROM")
//$rndabc = RNDFROM("Один", "Два", "Три")
//LOGWRITE ("Рандом Один,Два,Три: ", $rndabc)

LOGWRITE (" ")
LOGWRITE ("тест INT")
$INT_TEST = INT(25.73)
LOGWRITE ("Из 25.73 возвращает 25: ", $INT_TEST)

LOGWRITE (" ")
LOGWRITE ("тест DIST")
$DIST_TEST = DIST(100, 100, 300, 300)
LOGWRITE ("Возвращает 283: ", $DIST_TEST)

LOGWRITE (" ")
LOGWRITE ("тест SQRT")
$SQRT_TEST = SQRT(400)
LOGWRITE ("Возвращает 20: ", $SQRT_TEST)

LOGWRITE (" ")
LOGWRITE ("тест ROUND")
$ROUND_TEST = ROUND(1236, 1) // $r = 1240
LOGWRITE ("Возвращает 1240: ", $ROUND_TEST)
$ROUND_TEST = ROUND(1236, 2) // $r = 1200
LOGWRITE ("Возвращает 1200: ", $ROUND_TEST)
$ROUND_TEST = ROUND(1.236, -2) // $r = 1.24
LOGWRITE ("Возвращает 1.24: ", $ROUND_TEST)
$ROUND_TEST = ROUND(1.236, -1) // $r = 1.2
LOGWRITE ("Возвращает 1.2: ", $ROUND_TEST)
$ROUND_TEST = ROUND(1.236, 0)  // $r = 1
LOGWRITE ("Возвращает 1: ", $ROUND_TEST)

LOGWRITE (" ")
LOGWRITE ("======Тест строковых функций===========")
LOGWRITE (" ")

LOGWRITE ("тест INIWRITE, Запись данных")
INIWRITE ("Test INI.ini", "Param", "500")
INIWRITE ("Test INI.ini", "Max", "12345", "GENERAL")
INIWRITE ("Test INI.ini", "Min", "50", "GENERAL")
INIWRITE ("Test INI.ini", "Max", "2000", "NEW")
INIWRITE ("Test INI.ini", "Min", "60", "NEW")

LOGWRITE (" ")
LOGWRITE ("тест INIREAD, Чтение данных")
$INIREAD_TEST = INIREAD ("Test INI.ini", "Param")
LOGWRITE ("Возвращает 500: ", $INIREAD_TEST)
$INIREAD_TEST = INIREAD ("Test INI.ini", "Max", "GENERAL")
LOGWRITE ("Возвращает 12345: ", $INIREAD_TEST)
$INIREAD_TEST = INIREAD ("Test INI.ini", "Min", "GENERAL")
LOGWRITE ("Возвращает 50: ", $INIREAD_TEST)
$INIREAD_TEST = INIREAD ("Test INI.ini", "Max", "NEW")
LOGWRITE ("Возвращает 2000: ", $INIREAD_TEST)
$INIREAD_TEST = INIREAD ("Test INI.ini", "Min", "NEW")
LOGWRITE ("Возвращает 60: ", $INIREAD_TEST)

LOGWRITE (" ")
LOGWRITE ("тест STRLEN")
$STRLEN_TEST = STRLEN ("Привет")
LOGWRITE ("Возвращает 6: ", $STRLEN_TEST)

LOGWRITE (" ")
LOGWRITE ("тест STRCUT")
$STRCUT_TEST = STRCUT("Тест Привет Тест", 6, 6)
LOGWRITE ("Возвращает Привет: ", $STRCUT_TEST)

LOGWRITE (" ")
LOGWRITE ("тест STRCUT2")
$STRCUT2_TEST = STRCUT2("Тест Привет Тест", 6, 11)
LOGWRITE ("Возвращает Привет: ", $STRCUT2_TEST)

LOGWRITE (" ")
LOGWRITE ("тест STRFILTER Раз два три")
$STRFILTER_TEST = STRFILTER ("Раз два три", "три", 0)
LOGWRITE ("Вырезать три: ", $STRFILTER_TEST)

LOGWRITE (" ")
LOGWRITE ("тест STRFILTER Раз два три")
$STRFILTER_TEST = STRFILTER ("Раз два три", "три", 1)
LOGWRITE ("Вырезать все кроме три: ", $STRFILTER_TEST)

LOGWRITE (" ")
LOGWRITE ("тест STRPOS")
$STRPOS_TEST = STRPOS ("Раз два три", "три")
LOGWRITE ("Возвращает 9: ", $STRPOS_TEST)

LOGWRITE (" ")
LOGWRITE ("тест STRCONCAT")
$STRCONCAT_TEST = STRCONCAT ("1 ", $STRPOS_TEST, " 50")
LOGWRITE ("Возвращает 1 9 50: ", $STRCONCAT_TEST)

LOGWRITE (" ")
LOGWRITE ("тест STRREPLACE Раз два три")
$STRREPLACE_TEST = STRREPLACE ("Раз два три", "два", "десять")
LOGWRITE ("Возвращает Раз десять три: ", $STRREPLACE_TEST)

LOGWRITE (" ")
LOGWRITE ("тест STRWRITELN Запись Тест Тест Тест")
STRWRITELN ("Test_file.txt", "Тест Тест Тест")

LOGWRITE (" ")
LOGWRITE ("тест STRREADLN Чтение Тест Тест Тест")
$STRREADLN_TEST = STRREADLN ("Test_file.txt", 1)
LOGWRITE ($STRREADLN_TEST)

LOGWRITE (" ")
LOGWRITE ("тест STRMD5 Хеш 30CA2C8DE471253478A986EAF36307B3")
$STRMD5_TEST = STRMD5 ($STRREADLN_TEST)
LOGWRITE ($STRMD5_TEST)

LOGWRITE (" ")
LOGWRITE ("Запись Тест тесТ в буфер")
TOCLIP ("Тест тесТ")

LOGWRITE (" ")
LOGWRITE ("Чтение из буфера Тест тесТ")
$FROMCLIP_TEST = FROMCLIP ()
LOGWRITE ($FROMCLIP_TEST)

LOGWRITE (" ")
LOGWRITE ("тест HTTPGET")
$HTTPGET_TEST = HTTPGET("http://crapware.aidf.org/version/clickermann.php", "Referrer: http://crapware.aidf.org")
LOGWRITE ("Возвращает версию кликера: ", $HTTPGET_TEST)

LOGWRITE (" ")
LOGWRITE ("тест TFWRITE Запись Test Тест 2а раза")
TFWRITE ("Test_TFWRITE.txt", "Test Тест 1")
TFWRITE ("Test_TFWRITE.txt", "Test Тест 2")

LOGWRITE (" ")
LOGWRITE ("тест TFREAD Чтение Test Тест 2а раза")
$TFREAD_TEST = TFREAD ("Test_TFWRITE.txt", 1)
LOGWRITE ("Возвращает Test Тест 1: ", $TFREAD_TEST)
$TFREAD_TEST = TFREAD ("Test_TFWRITE.txt", 2)
LOGWRITE ("Возвращает Test Тест 2: ", $TFREAD_TEST)

LOGWRITE (" ")
LOGWRITE ("тест TFDELETE Удаление Test Тест 2а раза...")
LOGWRITE ("...и запись TeStTeSt 2а раза")
TFDELETE ("Test_TFWRITE.txt", 2)
TFDELETE ("Test_TFWRITE.txt", 1)
TFWRITE ("Test_TFWRITE.txt", "TeStTeSt 1")
TFWRITE ("Test_TFWRITE.txt", "TeStTeSt 2")
$TFREAD_TEST = TFREAD ("Test_TFWRITE.txt", 1)
LOGWRITE ("Возвращает TeStTeSt 1: ", $TFREAD_TEST)
$TFREAD_TEST = TFREAD ("Test_TFWRITE.txt", 2)
LOGWRITE ("Возвращает TeStTeSt 2: ", $TFREAD_TEST)

LOGWRITE (" ")
LOGWRITE ("тест TFCOUNT ко-во строк из файла Test_TFWRITE.txt")
$TFCOUNT_TEST = TFCOUNT ("Test_TFWRITE.txt")
LOGWRITE ("Возвращает 2: ", $TFCOUNT_TEST)


LOGWRITE (" ")
LOGWRITE ("Тест чтения и разбора строк из файла")
$count = 0
FOR ($count, $count<TFCOUNT("LogPass.txt"))
   $str = TFREAD ("LogPass.txt", $count+1)
   $log = STRCUT2 ($str, 1, STRPOS ($str, ":")-1)
   $pass = STRCUT2 ($str, STRPOS ($str, ":")+1, STRLEN ($str))
   LOGWRITE ("Логин: ", $log, " Пароль: ", $pass)
   WAITMS(100)
end_cyc

LOGWRITE (" ")
LOGWRITE ("Тест TFCLEAR")
IF(TFCOUNT ("Test_file.txt") > 0)
   TFCLEAR ("Test_file.txt", 1)
END_IF
IF(TFCOUNT ("Test_TFWRITE.txt") > 0)
   TFCLEAR ("Test_TFWRITE.txt", 1)
END_IF
IF(TFCOUNT ("Test INI.ini") > 0)
   TFCLEAR ("Test INI.ini", 1)
END_IF
LOGWRITE ("Файлы удалены")

LOGWRITE (" ")
LOGWRITE ("======Тест Условий и Циклов============")
LOGWRITE (" ")

LOGWRITE ("Тест Условия (верно, не верно)")
$a = 1
$b = 2
IF(($a = 1) & ($b = 2))
   LOGWRITE ("Условие верно")
ELSE
   LOGWRITE ("Условие не верно")
END_IF

$a = 1
$b = 1
IF(($a = 1) & ($b = 2))
   LOGWRITE ("Условие верно")
ELSE
   LOGWRITE ("Условие не верно")
END_IF

LOGWRITE (" ")
LOGWRITE ("Тест FOR")
FOR($x, $x < 3)
   FOR($y, $y < 3)
      print($x, ":", $y)
   END_CYC
END_CYC

LOGWRITE (" ")
LOGWRITE ("Тест WHILE")
LOGWRITE ("Тест простого цикла")

$f_test = 1
while($f_test < 5)
   logwrite($f_test)
   inc($f_test)
end_cyc

LOGWRITE ("Тест цикла с комплексным условием")

$x=0
$y=0

while( ($x < 5) & ($y < 5) )
   print($x, ",", $y)
   inc($x, 1)
   inc($y, 1)
end_cyc

print($x,",",$y)

LOGWRITE (" ")
LOGWRITE ("Тест SUB 1,2")

GOSUB(sub_name)
LOGWRITE ("SUB 2")

SUB (sub_name)
LOGWRITE ("SUB 1")
END_SUB 

LOGWRITE (" ")
LOGWRITE ("======Тест функций анализа экрана======")
LOGWRITE (" ")

GETSCREEN
IF_PIXEL_IN(0,0, $_xmax,$_ymax, 3761085)
   LOGWRITE ("Тест IF_PIXEL_IN X= ", $_return1, ", Y= ", $_return2)
   MOVE($_return1, $_return2)
   WAITMS(500)
   MOVER(50, 50)
Else
   LOGWRITE ("Тест IF_PIXEL_IN: Пиксел не найден!")
END_IF

IF_PICTURE_IN (0,0, $_xmax,$_ymax, "Test.bmp")
   LOGWRITE ("Тест IF_PICTURE_IN X= ", $_return1, ", Y= ", $_return2)
   MOVE($_return1, $_return2)
   WAITMS(500)
   MOVER(50, 50)
Else
   LOGWRITE ("Тест IF_PICTURE_IN: Картинка не найдена!")
END_IF





LOGWRITE (" ")
LOGWRITE ("======СТОП=============================")
LOGSHOW (1)
HALT